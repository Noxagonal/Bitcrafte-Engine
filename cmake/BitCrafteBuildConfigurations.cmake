
include(CheckIPOSupported)

if(NOT DEFINED BITCRAFTE_BUILD_CONFIGURATIONS_INCLUDED)
	set(BITCRAFTE_BUILD_CONFIGURATIONS_INCLUDED TRUE)


	# Set BitCrafte build configurations.
	macro(set_bitcrafte_build_configurations)

		set(CMAKE_CONFIGURATION_TYPES "GameShipping;GameDevelopment;EngineDevelopment" CACHE STRING "" FORCE)

		set(CMAKE_CXX_FLAGS_GAMESHIPPING "${CMAKE_CXX_FLAGS_RELEASE}")
		set(CMAKE_C_FLAGS_GAMESHIPPING "${CMAKE_C_FLAGS_RELEASE}")
		set(CMAKE_EXE_LINKER_FLAGS_SHIPPING "${CMAKE_EXE_LINKER_FLAGS_RELEASE}")
		set(CMAKE_SHARED_LINKER_FLAGS_SHIPPING "${CMAKE_SHARED_LINKER_FLAGS_RELEASE}")

		set(CMAKE_CXX_FLAGS_GAMEDEVELOPMENT "${CMAKE_CXX_FLAGS_RELWITHDEBINFO}")
		set(CMAKE_C_FLAGS_GAMEDEVELOPMENT "${CMAKE_C_FLAGS_RELWITHDEBINFO}")
		set(CMAKE_EXE_LINKER_FLAGS_GAMEDEVELOPMENT "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO}")
		set(CMAKE_SHARED_LINKER_FLAGS_GAMEDEVELOPMENT "${CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO}")

		set(CMAKE_CXX_FLAGS_ENGINEDEVELOPMENT "${CMAKE_CXX_FLAGS_DEBUG}")
		set(CMAKE_C_FLAGS_ENGINEDEVELOPMENT "${CMAKE_C_FLAGS_DEBUG}")
		set(CMAKE_EXE_LINKER_FLAGS_ENGINEDEVELOPMENT "${CMAKE_EXE_LINKER_FLAGS_DEBUG}")
		set(CMAKE_SHARED_LINKER_FLAGS_ENGINEDEVELOPMENT "${CMAKE_SHARED_LINKER_FLAGS_DEBUG}")

		# Set 'EngineDevelopment' as the default build type if none is specified
		if(NOT CMAKE_BUILD_TYPE)
			message(STATUS "Setting default build type to 'EngineDevelopment' as none was specified.")
			set(CMAKE_BUILD_TYPE "EngineDevelopment" CACHE STRING "Choose the type of build." FORCE)
			# Set the possible values of build type for cmake-gui, ccmake
			set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "${CMAKE_CONFIGURATION_TYPES}")
		endif()

	endmacro()


	function(set_interprocedural_optimization
		SET_INTERPROCEDURAL_OPTIMIZATION_ENABLED
	)
		if(${SET_INTERPROCEDURAL_OPTIMIZATION_ENABLED})
			check_ipo_supported(RESULT IPO_SUPPORTED_RESULT OUTPUT IPO_OUTPUT)
			if(NOT IPO_SUPPORTED_RESULT)
				message(WARNING "Interprocedural optimization is not supported: ${IPO_OUTPUT}")
			endif()
			set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ${IPO_SUPPORTED_RESULT} PARENT_SCOPE)
		else()
			set(CMAKE_INTERPROCEDURAL_OPTIMIZATION OFF PARENT_SCOPE)
		endif()
	endfunction()


endif()
